/*
 * Some things this build script file do:
 *     Java 1.8 required.
 *     A system property is set using key "gradle.test.temp.dir".
 *     Enable TestNG as unit test framework.
 *     Provide "run file" support for NetBeans by declaring task 'run'.
 *     Set compiler args "-Xlint:unchecked" and "-Xlint:deprecation".
 */

apply plugin: 'java'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

test {
    useTestNG()
    
    systemProperty 'gradle.test.temp.dir', temporaryDir as String
    
    testLogging.showStandardStreams = true
    
    // Always run the test task:
    outputs.upToDateWhen {
        false
    }
    
    doLast {
        println 'Done. See the report at: ' + reports.html.entryPoint
    }
}

repositories {
    jcenter()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.10'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
    description += " (version $gradleVersion)"
}

task run(type: JavaExec, dependsOn: compileJava) {
    description "Execute a specific class using a Gradle command-line argument like so \"-PmainClass=com.x.y.MyCLass\")."
    
    if (project.hasProperty('mainClass')) {
        main = mainClass
    }
    
    classpath = sourceSets.main.runtimeClasspath
}

// Source: http://stackoverflow.com/a/22183825
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}